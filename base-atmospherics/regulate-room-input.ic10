push HASH("GS Hab")
push HASH("VP Hab Oxygen")
push HASH("VP Hab Nitrogen")
alias arrayEnd r15
move arrayEnd sp
define PFVP -321403609
define PFGS -1252983604
define targetLowPartialO2 20 # kPa
define targetHighPartialO2 22 # kPa
define targetLowPartialN2 76 # kPa
define targetHighPartialN2 78 # kPa
define lerpRange 2 # kPa
define maxPressure 120 # kPa
alias gr0 r0
alias gr1 r1
alias pressure r2
alias nhGS r3
alias nhVPOxy r4
alias nhVPNit r5
alias watermarkState r6

loop:
yield
move sp arrayEnd
eachRoom:
blez sp loop # End of entries?
pop nhVPNit
pop nhVPOxy
pop nhGS
lbn pressure PFGS nhGS Pressure Average
# Check to prevent overpressure.
bgt pressure maxPressure stopInput 
# Check if should pump Oxygen.
lbn gr0 PFGS nhGS RatioOxygen Average
mul gr0 pressure gr0 # Partial Oxygen pressure
slt gr1 gr0 targetLowPartialO2
lbn watermarkState PFVP nhVPOxy On Maximum
or watermarkState watermarkState gr1
slt gr1 gr0 targetHighPartialO2
and watermarkState watermarkState gr1
sub gr0 targetHighPartialO2 gr0
div gr0 gr0 lerpRange
lerp gr0 1 10 gr0
sbn PFVP nhVPOxy On watermarkState
sbn PFVP nhVPOxy Setting gr0
# Check if should pump Nitrogen.
lbn gr0 PFGS nhGS RatioNitrogen Average
mul gr0 pressure gr0 # Partial Nitrogen pressure
slt gr1 gr0 targetLowPartialN2
lbn watermarkState PFVP nhVPNit On Maximum
or watermarkState watermarkState gr1
slt gr1 gr0 targetHighPartialN2
and watermarkState watermarkState gr1
sub gr0 targetHighPartialN2 gr0
div gr0 gr0 lerpRange
lerp gr0 1 10 gr0
sbn PFVP nhVPNit On watermarkState
sbn PFVP nhVPNit Setting gr0
j eachRoom

stopInput:
sbn PFVP nhVPOxy On 0
sbn PFVP nhVPNit On 0
j eachRoom